USE dbIMS
go
IF OBJECT_ID('dbo.spUTGetVesselCodingTemp') IS NOT NULL
BEGIN
    DROP PROCEDURE dbo.spUTGetVesselCodingTemp
    IF OBJECT_ID('dbo.spUTGetVesselCodingTemp') IS NOT NULL
        PRINT '<<< FAILED DROPPING PROCEDURE dbo.spUTGetVesselCodingTemp >>>'
    ELSE
        PRINT '<<< DROPPED PROCEDURE dbo.spUTGetVesselCodingTemp >>>'
END
go
create procedure dbo.spUTGetVesselCodingTemp

/* Procedure name: spUTGetVesselCodingTemp

   Parameters:
    Input:    ProductCode, PackageCode, StockCode, PlantAreaId, RequestDate (optional)
    Output:    A result set containing potentially multiple rows of the column @PrintedCode - char(25)

   Called Stored Procedures:
    SpUTGetSCBCode
    NOTE: spUTGetVesselCodingTemp, spUTGetCarrierCoding, and spUTGetVesselCodingTemp
          all determine @FormatCode then call spUTGetSCBCode

   Return the vessel coding required for the given PCS, line & date.
   Default the date to current date if it is not given.

   The formatting code used is determined by the mapping of Stock Code Book material types
   to the BOM Material Types.  The first code found that corresponds to a BOM
   Material Type of NOT("CORR", "CTN", "STCS" or "TRAY") and NOT like ("CARR") is used.

   To find a format code, check in PCSCodingFormat first, then in StockCodingFormat if not found.
   This means any coding specified for the PCS is used instead of coding specified for the Stock Code.

   Written by: JDH & DMD 1/3/97

Modifications:
    DMD - 1/27/98 : added check for leading space in stock code

    DMD:002 - 8/16/04 : Only look in StockCodingFormat when there are no entries for a PCS in
                    PCSCodingFormat, regardless of StockCodeBookMaterialType.  If there are entries
                    in PCSCodingFormat, those are the only valid StockCodeBookMaterialTypes, don't DEFAULT
                    to StockCodingFormat.
    EG - 11/29/04 : Phased out old tables: PCSCodingFormat, StockCodingFormat, StockCodeBook.
*/
(
    @ProductCode    char(2),
    @PackageCode    char(3),
    @StockCode    char(3),
    @PlantAreaId    char(4),
    @RequestDate    datetime
)

as

declare	@FormatCode char(4),
	@PCS_ID     numeric(9,0)

if @RequestDate is null
    select @RequestDate = getdate()

/* Stock Code Book uses a two character stock code without the "-" or the "R" */
if substring(@StockCode,1,1) in ("-", "R", " ") and datalength(@StockCode) = 3
    select @StockCode = rtrim(substring(@StockCode,2,2))

select @FormatCode = null

/* Determine which FormatCode to use     */
/* First, check the full PCS        */
IF @ProductCode IS NOT NULL OR @ProductCode <> ""
	SELECT	@PCS_ID = PCS_ID
	FROM	SCB_PCS_CD (INDEX indxSCB_PCS_CD_ByPCS)
	WHERE	PROD_CD = @ProductCode AND
		CONT_CD = @PackageCode AND
		STCK_CD = @StockCode   AND
		EXISTS
		(	SELECT	1
			FROM	SCB_STCK_CD_MATL
			WHERE	PCS_ID    = SCB_PCS_CD.PCS_ID AND
				START_DT <= @RequestDate     AND
				(END_DT IS NULL OR END_DT >= @RequestDate)
		)

SET	rowcount 1  -- <=== This is a precaution in case there are multiple carrier codes

IF @PCS_ID IS NOT NULL
	SELECT	@FormatCode = CONVERT(CHAR(4),scm.PRT_CD_ID)
	FROM	SCB_STCK_CD_MATL          scm,
		StockCodeBookMaterialType scbmt
	WHERE	scm.PCS_ID    = @PCS_ID       AND
		scm.START_DT <= @RequestDate AND
		(scm.END_DT IS NULL OR scm.END_DT >= @RequestDate) AND
		scbmt.SCBMaterialTypeCode = scm.PRT_CD_MATL_ID     AND
		scbmt.MaterialTypeCode NOT IN ('CORR', 'CTN', 'STCS', 'TRAY') AND
		scbmt.MaterialTypeCode NOT LIKE 'CARR%'
ELSE -- If a FormatCode for the PCS is not found, search using the Stock Code only
	SELECT	@FormatCode = CONVERT(CHAR(4),PRT_CD_ID)
	FROM	SCB_PCS_CD       pcs (INDEX indxSCB_PCS_CD_ByPCS),
		SCB_STCK_CD_MATL scm,
		StockCodeBookMaterialType scbmt
	WHERE	PROD_CD    IS NULL           AND
		CONT_CD    IS NULL           AND
		STCK_CD       = @StockCode   AND
		scm.PCS_ID    = pcs.PCS_ID   AND
		scm.START_DT <= @RequestDate AND
		(scm.END_DT IS NULL OR scm.END_DT >= @RequestDate) AND
		scbmt.SCBMaterialTypeCode = scm.PRT_CD_MATL_ID     AND
		scbmt.MaterialTypeCode NOT IN ('CORR', 'CTN', 'STCS', 'TRAY') AND
		scbmt.MaterialTypeCode NOT LIKE 'CARR%'

SET	rowcount 0

exec spUTGetSCBCoding
    @ProductCode,
    @PackageCode,
    @StockCode,
    @PlantAreaId,
    @RequestDate,
    @FormatCode
go
EXEC sp_procxmode 'dbo.spUTGetVesselCodingTemp', 'unchained'
go
IF OBJECT_ID('dbo.spUTGetVesselCodingTemp') IS NOT NULL
    PRINT '<<< CREATED PROCEDURE dbo.spUTGetVesselCodingTemp >>>'
ELSE
    PRINT '<<< FAILED CREATING PROCEDURE dbo.spUTGetVesselCodingTemp >>>'
go
GRANT EXECUTE ON dbo.spUTGetVesselCodingTemp TO MES_CODER_RO
go
GRANT EXECUTE ON dbo.spUTGetVesselCodingTemp TO ims_firecall_role
go
GRANT EXECUTE ON dbo.spUTGetVesselCodingTemp TO ims_rw_role
go
